// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.google.gms:google-services:3.0.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


allprojects {
    repositories {
        jcenter()
    }
}


task launchCapstone{
    dependsOn = ["startRecipesServer", "buildCookingWithCookeo", "deployCookingWithCookeo", "startCookingWithCookeo"]
    evaluationDependsOn ":recipesserver"

    project(":recipesserver"){
        appengine{
            daemon true
        }
    }

    doLast{
        println "Job Done"
    }

}

task buildCookingWithCookeo{
    dependsOn "app:assembleRelease"
    mustRunAfter "startRecipesServer"
}

task deployCookingWithCookeo(type: Exec) {
    mustRunAfter "buildCookingWithCookeo"

    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            instr -> properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def adb = "$sdkDir/platform-tools/adb"
        commandLine "$adb", 'install', './app/build/outputs/apk/app-release.apk'
    }
}

task startCookingWithCookeo(type: Exec) {
    mustRunAfter "deployCookingWithCookeo"

    def rootDir = project.rootDir
    def localProperties = new File(rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            instr -> properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def adb = "$sdkDir/platform-tools/adb"
        commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.rukiasoft.androidapps.cocinaconroll/.ui.RecipeListActivity'
    }
}

task startRecipesServer{
    dependsOn "recipesserver:appengineRun"
}

task stopRecipesServer{
    dependsOn "recipesserver:appengineStop"
}

